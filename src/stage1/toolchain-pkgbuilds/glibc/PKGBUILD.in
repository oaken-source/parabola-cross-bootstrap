# Maintainer (Arch): Anatol Pomozov

_target=@CHOST@
pkgname=$_target-glibc
pkgver=2.27
pkgrel=1
_commit=23158b08a0908f381459f273a984c6fd328363cb
pkgdesc="GNU C Library @CARCH@ target"
arch=('x86_64')
url='http://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
depends=($_target-gcc $_target-linux-api-headers)
options=(!buildflags !strip staticlibs)
source=(http://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig})
sha1sums=('1f7a9c43026484943ef7cf6885f2176a2bc1e093'
          'SKIP')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8)  # "Carlos O'Donell <carlos@systemhalted.org>"

prepare() {
  mkdir -p glibc-build lib32-glibc-build
}

build() {
  local _configure_flags=(
      --prefix=/usr
      --disable-werror
      --enable-shared
      --enable-obsolete-rpc
      --with-headers=/usr/$_target/include
      --@MULTILIB@-multilib
      --enable-kernel=3.0.0
  )

  cd "$srcdir/glibc-build"

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  # remove fortify for building libraries
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  export BUILD_CC=gcc
  export CC="${_target}-gcc @PLATFORM_CFLAGS@"
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

	CFLAGS="$CFLAGS -g -O2 @PLATFORM_CFLAGS@" \
	ASFLAGS="$ASFLAGS @PLATFORM_CFLAGS@" \
  "$srcdir/glibc-$pkgver/configure" \
      --host=$_target \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      ${_configure_flags[@]}

  # build libraries with fortify disabled
  echo "build-programs=no" >> configparms
  make

  # re-enable fortify for programs
  sed -i "/build-programs=/s#no#yes#" configparms

  echo "CC += -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -D_FORTIFY_SOURCE=2" >> configparms
  make

  [ "x@MULTILIB@" == "xenable" ] || return 0

  cd "$srcdir/lib32-glibc-build"
  export CC="${_target}-gcc @PLATFORM32_CFLAGS@"

  echo "slibdir=/usr/lib32" >> configparms
  echo "rtlddir=/usr/lib32" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  # remove fortify for building libraries
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}

	CFLAGS="$CFLAGS -mcmodel=medlow -g -O2 @PLATFORM32_CFLAGS@" \
	ASFLAGS="$ASFLAGS -mcmodel=medlow @PLATFORM32_CFLAGS@" \
  "$srcdir/glibc-$pkgver/configure" \
      --host=@CHOST32@ \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib32 \
      ${_configure_flags[@]}

  # build libraries with fortify disabled
  echo "build-programs=no" >> configparms
  make

  # re-enable fortify for programs
  sed -i "/build-programs=/s#no#yes#" configparms

  echo "CC += -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -D_FORTIFY_SOURCE=2" >> configparms
  make

}

package() {
  cd glibc-build

  make install_root=$pkgdir/usr/$_target install

  [ "x@MULTILIB@" == "xenable" ] || return 0

  cd lib32-glibc-build

  make install_root=$pkgdir/usr/$_target install
}
